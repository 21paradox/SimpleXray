apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    namespace "com.simplexray.an"
    compileSdk 35

    defaultConfig {
        applicationId "com.simplexray.an"
        versionCode 10
        versionName "1.4.3"
        setProperty("archivesBaseName", "simplexray")
        ndk {
            abiFilters "arm64-v8a", "x86_64"
        }
        externalNativeBuild {
            ndkBuild {
                arguments "APP_CFLAGS+=-DPKGNAME=com/simplexray/an -ffile-prefix-map=${rootDir}=."
                arguments "APP_LDFLAGS+=-Wl,--build-id=none"
            }
        }
        targetSdk 35
        minSdk 29
    }

    signingConfigs {
        release {
            enableV1Signing false
            enableV2Signing false
            enableV3Signing true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    def propsFile = rootProject.file('store.properties')
    def configName = 'release'

    if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props != null && props.containsKey('storeFile')) {
            android.signingConfigs[configName].storeFile = rootProject.file(props['storeFile'])
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
    ndkVersion '29.0.13113456 rc1'
    kotlinOptions {
        jvmTarget = '1.8'
    }

    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a", "x86_64"
            universalApk true
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.each { output ->
            def abiFilter = output.getFilter("ABI")
            if (abiFilter != null) {
                output.outputFileName = "${archivesBaseName}-${abiFilter}.apk"
            } else {
                output.outputFileName = "${archivesBaseName}-universal.apk"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.core:core-ktx:1.16.0'
}
